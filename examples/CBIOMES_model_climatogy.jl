### A Pluto.jl notebook ###
# v0.16.4

using Markdown
using InteractiveUtils

# ‚ïî‚ïê‚ï° 4e55fa2e-3193-11ec-3a50-2defc49cc54d
begin
	using Pkg
	Pkg.activate()
	using NCTiles, MITgcmTools, MeshArrays, OceanColorData, OceanStateEstimation
	using PlutoUI, JLD2, CairoMakie
	import CairoMakie as Mkie
	"Done with packages"
end

# ‚ïî‚ïê‚ï° 979a1b66-0aaa-4b8f-868c-85db76d3fb99
md"""# Interpolated Climatology Workflow

In this notebook we illustrate how `MeshArrays.jl` and `NCTiles.jl` can be used to:

- start from native-grid output from the [CBIOMES-global](https://cbiomes.readthedocs.io/en/latest/)
- read from file, derive, and interapolate new variables
- write results to CF-compliant NetCDF files
- plot result maps interactively (TBD)

`Author: Ga√´l Forget`

`Date: 2021/10/25`
"""

# ‚ïî‚ïê‚ï° aa41a861-7eca-4dda-92e8-bd8e96f2a21e
PlutoUI.TableOfContents()

# ‚ïî‚ïê‚ï° 5fcb77a8-b234-45d9-a4bd-02bd4eb8fbd9
md"""## Typical Workflow

1. encode functions like `‚ñ∂a(x,t)`, `‚ñ∂b(x,t)` to derive target variables
   - input: `x` is path to input variable folder (`nctiles`); `t"` is time record
   - output: one two-dimensional field (the derivation end-result)
1. loop over target variables
   - output result to temporary storage (`jld2` files)
   - create and instantiate new netcdf files (`nc` files)
1. combine all `nc` files into one

!!! note
	- the first time, this notebook generates `half_degree_grid_coeffs.jld2` which may add $O(1')$ runtime
    - derivation methods ‚ñ∂c, to ‚ñ∂g illustrate different input/output setups (e.g. N inputs to M outputs)
    - to create and instantiate `nc` files, we define `NCTiles.NCT` instances and simply call `NCTiles.write()`
    - could use infinite time dimension and increment file instead to avoid reading 12 fields at once
"""

# ‚ïî‚ïê‚ï° ad9d39a2-65b3-4fd9-aecd-f360ac34df0f
md"""## Configuration"""

# ‚ïî‚ïê‚ï° d7d0fc22-0bd7-4c4b-9249-8084d75bb33e
md"""## Derivation Methods

The functions provided below derive the target variables listed hereafter.

| Variables [1/3] | Variables [2/3]  | Variables [3/3]  |
|:----------------|:----------------:|-----------------:|
| lon             | lat              |                  |
| SST             | SSS              | Chl              |
| MLD             | EuphoticDepth    | OceanDepth       |
| WindSpeed       | PAR              | TKE              |
| Rrs412          | Rrs443           | Rrs490           |
| Rrs510          | Rrs555           | Rrs670           |

_(note: could generate via `PrettyTables.jl` and `with__terminal` instead)_

### Interpolation, Subsampling
"""

# ‚ïî‚ïê‚ï° 7a1058e4-3813-4b90-a9ee-ad5ff4eb6d6f
begin
	Œ≥=GridSpec("LatLonCap",MeshArrays.GRID_LLC90)
	Œì=GridLoad(Œ≥; option="full");
	"Done with reading grid files"
end

# ‚ïî‚ïê‚ï° 3bc4c68a-360d-4dae-bf03-a630d5bc7f24
begin
	lon=[i for i=-179.75:0.5:179.75, j=-89.75:0.5:89.75]
	lat=[j for i=-179.75:0.5:179.75, j=-89.75:0.5:89.75]	
	fil_coeffs=joinpath(tempdir(),"half_degree_grid_coeffs.jld2")
	if !isfile(fil_coeffs)
		(f,i,j,w,j_f,j_x,j_y)=InterpolationFactors(Œì,vec(lon),vec(lat))
		jldsave(fil_coeffs;f,i,j,w,j_f,j_x,j_y)
	end
	C=load(fil_coeffs)
	"Done with interpolation coefficients"
end

# ‚ïî‚ïê‚ï° 6eb8c05c-2624-4535-906b-3e38d502c997
begin	
	function ‚ñ∂a(x::String,t::Int) #Œì,lon,C from calling scope
		y=basename(x)
		X=read_nctiles(x,y,Œ≥;I=(:,:,1,t))[:,1]
		X[findall(Œì.hFacC[:,1] .== 0.)] .= NaN

		X=Interpolate(X,C["f"],C["i"],C["j"],C["w"])
		reshape(X,size(lon))
	end

	function ‚ñ∂b(x::String,t::Int) #Œì,lon,C from calling scope
		y=basename(x)
		X=read_nctiles(x,y,Œ≥;I=(:,:,t))[:,1]
		X[findall(Œì.hFacC[:,1] .== 0.)] .= NaN

		X=Interpolate(X,C["f"],C["i"],C["j"],C["w"])
		reshape(X,size(lon))
	end
	
	(‚ñ∂a,‚ñ∂b)
end

# ‚ïî‚ïê‚ï° 2965efbb-a5b7-47eb-8a3b-0e8024d4b15f
md"""### Ocean Bottom Depth"""

# ‚ïî‚ïê‚ï° d0c9aace-6fd1-4f04-92dc-4eede6cd47eb
function ‚ñ∂c(x::String,t::Int) #Œì,lon,C from calling scope
	X=Interpolate(Œì.Depth,C["f"],C["i"],C["j"],C["w"])
	reshape(X,size(lon))
end

# ‚ïî‚ïê‚ï° b8c2d4e6-7cbe-467c-bc67-4dc14b7eaf72
md"""### Remotely Sensed Reflectances"""

# ‚ïî‚ïê‚ï° 9828a5be-83f9-49f8-8f26-6f65451d021b
md"""### Chl (all phyto-plankton, 0-50m)""" 

# ‚ïî‚ïê‚ï° ad30d090-7e49-4ad6-adbd-bcb77cbcb2f1
begin
	w50m=similar(Œì.hFacC); w50m.=0.0
	tot_w50m=similar(Œì.hFacC[:,1]); tot_w50m.=0.0
	
	for f in eachindex(w50m)
		ni=Œì.hFacC.fSize[f[1]][1]
		nj=Œì.hFacC.fSize[f[1]][2]
		for i in 1:ni, j in 1:nj
			Œì.RC[f[2]].>-50.0 ? w50m[f][i,j]=Œì.hFacC[f][i,j]*Œì.DRF[f[2]] : nothing
			Œì.RC[f[2]].>-50.0 ? tot_w50m[f[1]][i,j]+=Œì.hFacC[f][i,j]*Œì.DRF[f[2]] : nothing
		end
	end

	for f in eachindex(w50m)
		w50m[f]=w50m[f]./tot_w50m[f[1]]
	end
	
	w50m[findall(isnan.(w50m))]=0.0
	
	"Done with ‚ñ∂e configuration"
end

# ‚ïî‚ïê‚ï° a3e1631a-56cc-428f-82ad-96e20d8e70d5
md"""### Surface PAR, Euphotic Depth"""

# ‚ïî‚ïê‚ï° abb34421-c340-4f22-a75f-4a23d6af03dc
function ‚ñ∂f(x::String,t::Int) #Œì,lon,C from calling scope
	y=basename(x)
	X=read_nctiles(x,y,Œ≥;I=(:,:,1,t))[:,1]
	X[findall(Œì.hFacC[:,1] .== 0.)] .= NaN
	
	#convert from uEin/m^2/s to Ein/m^2/day
	X=86400.0/1000000.0*X

	X=Interpolate(X,C["f"],C["i"],C["j"],C["w"])
	reshape(X,size(lon))
end

# ‚ïî‚ïê‚ï° fc858f0d-fed8-4f8d-a816-46b007157207
function ‚ñ∂g(x::String,t::Int) #Œì,lon,C from calling scope
	y=basename(x)
	X=read_nctiles(x,y,Œ≥;I=(:,:,:,t))
	X=write(X)
	
	ni=Œì.XC.grid.ioSize[1]
	nj=Œì.XC.grid.ioSize[2]
	euphotic_depth=fill(0.0,ni,nj)
	for i in 1:ni, j in 1:nj
		tmp1=X[i,j,:]; tmp2=0.01*tmp1[1]
		if tmp1[1]>0.0
			k1=minimum(findall(tmp1.<tmp2))
			tmp3=(tmp1[k1-1]-tmp2)/((tmp1[k1-1]-tmp1[k1]))
			euphotic_depth[i,j]=(1-tmp3)*Œì.RF[k1-1]+tmp3*Œì.RF[k1]
		end
	end	
	X=-read(euphotic_depth,Œì.XC)

	X[findall(Œì.hFacC[:,1] .== 0.)] .= NaN
	X=Interpolate(X,C["f"],C["i"],C["j"],C["w"])
	reshape(X,size(lon))
end

# ‚ïî‚ïê‚ï° ec8fb91c-1b11-4a81-8fac-08a5f3b4e9b3
md"""## Main Computation Loops

### Individual Variables
"""

# ‚ïî‚ïê‚ï° 9006655e-3b58-47d3-9baf-6b287e95ece1
md""" ### Remotely Sensed Reflectances"""

# ‚ïî‚ïê‚ï° 7015ddff-66b3-4c2c-bf9a-ccfc18c3693e
md""" ### Chl Concentration"""

# ‚ïî‚ïê‚ï° 4d8ded5f-cfd1-45ac-815c-b0358806e846
md"""## File Creation Loops

### Individual Variables
"""

# ‚ïî‚ïê‚ï° b59d16e0-477c-44f3-8e0a-6fcedadd0a23
md"""### Remotely Sensed Reflectances"""

# ‚ïî‚ïê‚ï° 4d29a7f6-6071-4dd3-b422-3d9c513b0d86
md"""### Combine NetCDF files"""

# ‚ïî‚ïê‚ï° 8b202092-62fd-46a1-a184-5a7a3cf40c46
"""
    CBIOMES_combine_files(fil_out::String)

Take all files generated earlier and combine them into one.
"""
function CBIOMES_combine_files(fil_out::String)

fil_out=joinpath(tempdir(),"CBIOMES_clim.nc")
list_in=["Chl","EuphoticDepth","MLD","OceanDepth","PAR",
    "Rrs412","Rrs443","Rrs490","Rrs510","Rrs555","Rrs670",
    "SSS","SST","TKE","WindSpeed"]

##

!isfile(fil_out) ? cp(joinpath(tempdir(),"SST_clim.nc"),fil_out) : nothing

for ii in list_in
    ds = NCTiles.Dataset(fil_out,"a")
    if !haskey(ds,ii)
        fil_in=joinpath(tempdir(),ii*"_clim.nc")
        ds_in = NCTiles.Dataset(fil_in,"r")
        tmp11=ds_in[ii][:]
        u=ds_in[ii].attrib["units"]
        ln=ds_in[ii].attrib["long_name"]
        close(ds_in)

        ##

        v = NCTiles.defVar(ds,ii,Float64,("lon","lat","t"), 
        attrib = Dict("units" => u, "long_name" => ln))
        v[:] = tmp11
    end
    close(ds)
end

ds = NCTiles.Dataset(fil_out,"a")
ds.attrib["description,1"]="Source: Gael Forget"
ds.attrib["description,2"]="Product: CBIOMES-global climatology"
ds.attrib["description,3"]="Version: alpha"
if haskey(ds.attrib,"A")
    NCTiles.delete!(ds.attrib,"description")
    NCTiles.delete!(ds.attrib,"A")
    NCTiles.delete!(ds.attrib,"B")
end
close(ds)

"all set"
end

# ‚ïî‚ïê‚ï° 12a4b433-3722-4ebc-b75f-027539d979b0
md"""## Appendices


1. inspect a sample input file, and retrieve meta-data from it
1. single variable test and comparison with earlier-code result

"""

# ‚ïî‚ïê‚ï° 1318a437-81b2-4982-8f58-9273a5cc6df9
begin
	pth0="201805-CBIOMES-climatology/nctiles/"
	fil0="PhysicalOceanography/THETA/THETA"
	ncvars,ncdims,fileatts = readncfile(joinpath(pth0,fil0*".0058.nc"));
	"Done with reading meta data from file"
end

# ‚ïî‚ïê‚ï° 9fbe6a83-9ed4-4e13-ae9b-d2742d60cc09
begin
	wvbd_out=Float64.([412, 443, 490, 510, 555, 670])
	wvbd_in=Float64.([400,425,450,475,500,525,550,575,600,625,650,675,700])
	Rirr=[["Rirr00$i" for i in 1:9];["Rirr0$i" for i in 10:13]]

	function ‚ñ∂d(x::String,t::Int) #Œì,lon,C from calling scope
		tmp=fill(0.0,size(lon)...,13)
		Rrs=fill(0.0,size(lon)...,6)
		Chla=fill(0.0,size(lon)...)

		for ii in 1:length(Rirr)
			x = joinpath(pth0,"IrradianceReflectance",Rirr[ii],Rirr[ii])
			X=read_nctiles(x,Rirr[ii],Œ≥;I=(:,:,t))[:,1]
			X[findall(Œì.hFacC[:,1] .== 0.)] .= NaN
			X=Interpolate(X,C["f"],C["i"],C["j"],C["w"])
			tmp[:,:,ii]=reshape(X,size(lon))
		end

		for i in 1:size(tmp,1), j in 1:size(tmp,2)
			Rrs[i,j,:].=RemotelySensedReflectance(tmp[i,j,:],wvbd_in,wvbd_out)
			Chla[i,j]=RrsToChla(Rrs[i,j,:])
		end
		
		Rrs,Chla
	end
end

# ‚ïî‚ïê‚ï° cffa9cb4-8ddf-4fb4-9874-cd1817fa2aad
üèÅ3 = let
	clim_Chl=fill(0.0,size(lon)...,12)
	clim_Rrs=[fill(0.0,size(lon)...,12) for j in 1:13]

	for t in 1:12
		(Rrs,Chl)=‚ñ∂d("Chl",t)
		clim_Chl[:,:,t].=Chl
		[clim_Rrs[j][:,:,t]=Rrs[:,:,j] for j in 1:6]
	end

	fil_Chl=joinpath(tempdir(),"Chl_from_Rrs_clim.jld2")
	jldsave(fil_Chl;clim_Chl)

	fil_Rrs=joinpath(tempdir(),"Rrs_clim.jld2")
	jldsave(fil_Rrs;clim_Rrs)

end

# ‚ïî‚ïê‚ï° d30cf2f5-5918-458c-be0c-900700642974
üìÅ2 = let
	üèÅ3

	meta1=[(name="Chl",units="mg/m^3",long_name="Chlorophyll a Concentration")
		(name="Rrs412",units="sr-1",long_name="Remote Sensing Reflectance at 412 nm")
		(name="Rrs443",units="sr-1",long_name="Remote Sensing Reflectance at 443 nm")
		(name="Rrs490",units="sr-1",long_name="Remote Sensing Reflectance at 490 nm")
		(name="Rrs510",units="sr-1",long_name="Remote Sensing Reflectance at 510 nm")
		(name="Rrs555",units="sr-1",long_name="Remote Sensing Reflectance at 555 nm")
		(name="Rrs670",units="sr-1",long_name="Remote Sensing Reflectance at 670 nm")]

	for ii in 1:length(meta1)
		nam=meta1[ii].name
		if ii==1
			data=load(joinpath(tempdir(),"Chl_from_Rrs_clim.jld2"))["clim_Chl"]
			outputfile=joinpath(tempdir(),"Chl_from_Rrs_clim.nc")
		else
			data=load(joinpath(tempdir(),"Rrs_clim.jld2"))["clim_Rrs"][ii-1]
			outputfile=joinpath(tempdir(),nam*"_clim.nc")
		end

		README = ["Source: Gael Forget","Product: CBIOMES-global climatology","Version: alpha"]
		meta=(outputfile=outputfile , README=README , yearrange=(1992,2011))
		meta=merge(meta1[ii],meta)
		
		nct=NCT(lon,lat,data,meta)
		write(nct)
	end	
end

# ‚ïî‚ïê‚ï° a009b061-839c-4820-bfe8-d73dcf909dfa
begin
	function ‚ñ∂e(x::String,t::Int) #Œì,lon,C from calling scope
		Chla=fill(0.0,size(lon)...)

		for ii in 1:35
			ii<10 ? nam="Chl0$(ii)" : nam="Chl$(ii)"
			x = joinpath(pth0,"Chlorophyll",nam,nam)
			X=read_nctiles(x,nam,Œ≥;I=(:,:,:,t))[:,1]
			
			Y=read( sum(write(X).*write(w50m),dims=3) , Œì.XC)
			Y[findall(Œì.hFacC[:,1] .== 0.)] .= NaN
			Y=Interpolate(Y,C["f"],C["i"],C["j"],C["w"])
			Y=reshape(Y,size(lon))
			
			Chla=Chla+Y
		end
		
		Chla
	end
end

# ‚ïî‚ïê‚ï° 6e389492-a103-419e-8513-9f22f1be4989
üèÅ4 = let
	clim=fill(0.0,size(lon)...,12)
	[clim[:,:,t]=‚ñ∂e("Chl",t) for t in 1:12]

	fil_Chl=joinpath(tempdir(),"Chl_clim.jld2")
	jldsave(fil_Chl;clim)
end

# ‚ïî‚ïê‚ï° 3d541a75-7d88-4343-aed1-c4639f67208d
begin
	üèÅ1=true
	PlutoUI.with_terminal() do
		input=["THETA","SALT","MXLDEPTH","EXFwspee","GGL90TKE"]
		output=["SST","SSS","MLD","WindSpeed","TKE"]
		‚ñ∂‚ñ∂ = [‚ñ∂a,‚ñ∂a,‚ñ∂b,‚ñ∂b,‚ñ∂a]
		pth=fill("PhysicalOceanography",5) 
		
		for ii in 1:length(input)
			X = joinpath(pth0,pth[ii],input[ii],input[ii])
			‚ñ∂‚ñ∂‚ñ∂ = ‚ñ∂‚ñ∂[ii]
			clim=fill(0.0,size(lon)...,12)
			[clim[:,:,t]=‚ñ∂‚ñ∂‚ñ∂(X,t) for t in 1:12]

			fil2=joinpath(tempdir(),output[ii]*"_clim.jld2")
			jldsave(fil2;clim)

			println("Done with pre-processing $(input[ii]) the 12 months")
		end
	end
end

# ‚ïî‚ïê‚ï° 7e912200-f852-457d-8fea-c05997f8ae1d
begin
	üèÅ2=true
	PlutoUI.with_terminal() do
		input=["OceanDepth","Chl","PARF","PARF"]
		output=["OceanDepth","Chl","PAR","EuphoticDepth"]
		‚ñ∂‚ñ∂ = [‚ñ∂c,‚ñ∂e,‚ñ∂f,‚ñ∂g]
		pth=[fill("PhysicalOceanography",1);"Chlorophyll";fill("IrradianceReflectance",2)] 
		
		for ii in 1:length(input)
			X = joinpath(pth0,pth[ii],input[ii],input[ii])
			‚ñ∂‚ñ∂‚ñ∂ = ‚ñ∂‚ñ∂[ii]
			clim=fill(0.0,size(lon)...,12)
			[clim[:,:,t]=‚ñ∂‚ñ∂‚ñ∂(X,t) for t in 1:12]

			fil2=joinpath(tempdir(),output[ii]*"_clim.jld2")
			jldsave(fil2;clim)

			println("Done with pre-processing $(input[ii]) the 12 months")
		end
	end
end

# ‚ïî‚ïê‚ï° 99fc0197-02d5-411b-a703-375fe71ca983
üìÅ1 = let
	üèÅ1, üèÅ2, üèÅ4
	meta0=[(name="SST",units="degC",long_name="Sea Surface Temperature (0-10m)")
		(name="SSS",units="psu",long_name="Sea Surface Salinity (0-10m)")
		(name="MLD",units="m",long_name="Mixed Layer Depth")
		(name="WindSpeed",units="m/s",long_name="Surface Wind Speed")
		(name="TKE",units="m^2/s^2",long_name="Turbulent Kinetic Energy (0-10m, from GGL90)")
		(name="OceanDepth",units="m",long_name="Bathymetry")
		(name="Chl",units="mg Chl",long_name="Chorophyll a (0-50m)")
		(name="PAR",units="m",long_name="PAR at Sea Surface")
		(name="EuphoticDepth",units="m",long_name="Euphotic Depth")]

	for ii in 1:length(meta0)
		nam=meta0[ii].name
		data=load(joinpath(tempdir(),nam*"_clim.jld2"))["clim"]
		outputfile=joinpath(tempdir(),nam*"_clim.nc")

		README = ["Source: Gael Forget","Product: CBIOMES-global climatology","Version: alpha"]
		meta=(outputfile=outputfile , README=README , yearrange=(1992,2011))
		meta=merge(meta0[ii],meta)
		
		nct=NCT(lon,lat,data,meta)
		write(nct)
	end	
end

# ‚ïî‚ïê‚ï° 0d28aa82-fa6f-466d-ba08-25016bce90a6
üìÅ3 = let
	üìÅ1, üìÅ2
	
	fil_out=joinpath(tempdir(),"CBIOMES-global-alpha-climatology.nc")
	rm(fil_out,force=true)
	CBIOMES_combine_files(fil_out)
end

# ‚ïî‚ïê‚ï° 9cd5900d-32f3-4a07-a4a2-071a0615c527
PlutoUI.with_terminal() do
	println("\n============ A look at "*fil0*".0058.nc using readncfile ========= \n\n")
	println("- Variables defined in ncvars: \n ")
	println.(keys(ncvars))
	println("\n")
	println("- Summary for some variables: \n ")
	show(ncvars["tim"])
	println("\n")
	show(ncvars["THETA"])
	println("\n")
end

# ‚ïî‚ïê‚ï° 8edb7bc3-c140-4e32-b626-d6333167ea06
let
	fil0="PhysicalOceanography/THETA/THETA"
	fil1=joinpath(pth0,fil0)

	SST=‚ñ∂a(fil1,1)

	fig = Mkie.Figure(resolution = (900,600), backgroundcolor = :grey95)
	ax = Mkie.Axis(fig[1,1], title="SST in degC",xlabel="longitude",ylabel="latitude")
	hm1=Mkie.contourf!(ax,lon[:,1],lat[1,:],SST,levels = -2.0:2.0:34.0, tickfont = (4, :black))
	xlims!(ax, (-180.0, 180.0)); ylims!(ax, (-90.0, 90.0))
	Mkie.Colorbar(fig[1,2], hm1, height = Mkie.Relative(0.65))

md"""### Single Month Test

!!! note
	Plot below shows SST for January after interpolation to the half-degree resolution grid.

$fig
"""
end

# ‚ïî‚ïê‚ï° d5ed2bcc-8034-4994-aaa7-2c5fbdfda37e
let
	üèÅ1
	
	t=11
	clim=load(joinpath(tempdir(),"SST_clim.jld2"))["clim"]
	pml=NCTiles.ncread("gridded_darwin_montly_clim_360_720_ver_0_2_6.nc","SST")

	RMSE=dropdims(sqrt.(sum((clim-pml).^2,dims=3)/12),dims=3)

	fig = Mkie.Figure(resolution = (900,600), backgroundcolor = :grey95)
	ax = Mkie.Axis(fig[1,1], title="log10(RMSE)",xlabel="longitude",ylabel="latitude")
	hm1=Mkie.contourf!(ax,lon[:,1],lat[1,:],log10.(RMSE), tickfont = (4, :black))
	xlims!(ax, (-180.0, 180.0)); ylims!(ax, (-90.0, 90.0))
	Mkie.Colorbar(fig[1,2], hm1, height = Mkie.Relative(0.65))

	md"""
	!!! note
	    Plot below shows room mean square difference from an earlier version generated with different software.
	
	$(fig)
	"""
end

# ‚ïî‚ïê‚ï° Cell order:
# ‚ïü‚îÄ979a1b66-0aaa-4b8f-868c-85db76d3fb99
# ‚ïü‚îÄaa41a861-7eca-4dda-92e8-bd8e96f2a21e
# ‚ïü‚îÄ5fcb77a8-b234-45d9-a4bd-02bd4eb8fbd9
# ‚ïü‚îÄad9d39a2-65b3-4fd9-aecd-f360ac34df0f
# ‚ï†‚ïê4e55fa2e-3193-11ec-3a50-2defc49cc54d
# ‚ïü‚îÄd7d0fc22-0bd7-4c4b-9249-8084d75bb33e
# ‚ïü‚îÄ7a1058e4-3813-4b90-a9ee-ad5ff4eb6d6f
# ‚ïü‚îÄ3bc4c68a-360d-4dae-bf03-a630d5bc7f24
# ‚ïü‚îÄ6eb8c05c-2624-4535-906b-3e38d502c997
# ‚ïü‚îÄ2965efbb-a5b7-47eb-8a3b-0e8024d4b15f
# ‚ïü‚îÄd0c9aace-6fd1-4f04-92dc-4eede6cd47eb
# ‚ïü‚îÄb8c2d4e6-7cbe-467c-bc67-4dc14b7eaf72
# ‚ïü‚îÄ9fbe6a83-9ed4-4e13-ae9b-d2742d60cc09
# ‚ïü‚îÄ9828a5be-83f9-49f8-8f26-6f65451d021b
# ‚ïü‚îÄad30d090-7e49-4ad6-adbd-bcb77cbcb2f1
# ‚ïü‚îÄa009b061-839c-4820-bfe8-d73dcf909dfa
# ‚ïü‚îÄa3e1631a-56cc-428f-82ad-96e20d8e70d5
# ‚ïü‚îÄabb34421-c340-4f22-a75f-4a23d6af03dc
# ‚ïü‚îÄfc858f0d-fed8-4f8d-a816-46b007157207
# ‚ïü‚îÄec8fb91c-1b11-4a81-8fac-08a5f3b4e9b3
# ‚ïü‚îÄ3d541a75-7d88-4343-aed1-c4639f67208d
# ‚ïü‚îÄ7e912200-f852-457d-8fea-c05997f8ae1d
# ‚ïü‚îÄ9006655e-3b58-47d3-9baf-6b287e95ece1
# ‚ïü‚îÄcffa9cb4-8ddf-4fb4-9874-cd1817fa2aad
# ‚ïü‚îÄ7015ddff-66b3-4c2c-bf9a-ccfc18c3693e
# ‚ïü‚îÄ6e389492-a103-419e-8513-9f22f1be4989
# ‚ïü‚îÄ4d8ded5f-cfd1-45ac-815c-b0358806e846
# ‚ïü‚îÄ99fc0197-02d5-411b-a703-375fe71ca983
# ‚ïü‚îÄb59d16e0-477c-44f3-8e0a-6fcedadd0a23
# ‚ïü‚îÄd30cf2f5-5918-458c-be0c-900700642974
# ‚ïü‚îÄ4d29a7f6-6071-4dd3-b422-3d9c513b0d86
# ‚ïü‚îÄ8b202092-62fd-46a1-a184-5a7a3cf40c46
# ‚ïü‚îÄ0d28aa82-fa6f-466d-ba08-25016bce90a6
# ‚ïü‚îÄ12a4b433-3722-4ebc-b75f-027539d979b0
# ‚ïü‚îÄ1318a437-81b2-4982-8f58-9273a5cc6df9
# ‚ïü‚îÄ9cd5900d-32f3-4a07-a4a2-071a0615c527
# ‚ïü‚îÄ8edb7bc3-c140-4e32-b626-d6333167ea06
# ‚ïü‚îÄd5ed2bcc-8034-4994-aaa7-2c5fbdfda37e
